#!/usr/bin/env wish

  package require Expect

  set Cmd /sbin/cryptsetup
  set Ask tmount-askpass.tk ;# should be in the same directory

  proc Dlg {} {
   global RetC
   set x [ expr { [ winfo screenwidth   . ] - [ winfo reqwidth  . ] } ]
   set x [ expr { $x / 2 - [ winfo vrootx . ] } ]
   set y [ expr { [ winfo screenheight  . ] - [ winfo reqheight . ] } ]
   set y [ expr { $y / 2 - [ winfo vrooty . ] } ]
   wm geom . +$x+$y ; wm deiconify . ; focus .ent ; tkwait var RetC
   catch { wm withdraw . } ; if { $RetC } { puts stderr Cancelled. };#fi
   return $RetC
  };# Dlg

  proc NoFS { Dev } {
    set P {E: ID_FS_USAGE=filesystem}
    return [ catch { exec udevadm info $Dev | grep -Fxq $P } ]
  };# NoFS

  proc GenMN { Dev } { ;# generate cryptsetups map name
    set M [ file tail $Dev ]
    set M [ regsub -all {[^[:alnum:]#+-.:=@]|[^[:ascii:]]} $M {_} ]
    return [ exec mktemp -u -p /dev/mapper "${M}-XXX" ]
  };# GenMN

  proc Prop { Dev N } { ;# get property for device
    if [ catch { exec lsblk -no $N $Dev } P ] { set P {} };#fi
    return [ set P ]
  }

  set RetC 1 ; set SN [ info script ]
  set Dev [ lindex $argv end ]
  set Lnk [ GenMN $Dev ] ; set Map [ file tail $Lnk ]
  set Cmd "sudo -A ${Cmd} open"
  set Mode [
    set M [ lindex $argv 0 ]
    if { $argc == 2 && $M in { -k -i -a } } { string range $M 1 1
    } elseif { $argc == 1 } { expr {{a}}
    };#fi
  ]

  if { $Mode eq {} } {
    puts stderr [
      format {Usage: %s [-k|-i|-a] {device|file}} [ file tail $SN ]
    ]
  } else {
    wm withdraw . ; wm title . tmount ; label .lbl ; frame .btn
    wm iconphoto . [ image create photo -file /usr/share/pixmaps/tmount.png ]
    bind . <Escape> { set RetC 1 } ; bind . <Destroy> { set RetC 1 }
    button .ok  -text OK     -command { set RetC 0 }
    button .esc -text Cancel -command { set RetC 1 }
    bind   .ok  <Return> { set RetC 0 } ; bind .esc <Return> { set RetC 1 }
    pack   .esc .ok -side right -in .btn
    pack   .btn -fill x -side bottom -padx 2 ; pack .lbl -fill x -padx 2
  };#fi

  if { $Mode eq {a} } {
    .lbl configure -text {LUKS passphrase input method:}
    frame .ent ; bind .ent <Return> { set RetC 0 }
    radiobutton .ent.i -var Mode -val i -text Interactive
    radiobutton .ent.k -var Mode -val k -text {Key File}
    pack .ent.i .ent.k -side left ; .ent.i select ; pack .ent -fill x -padx 2
    if [ Dlg ] { set Mode {} };#fi
    catch { destroy .ent }
  };#fi

  if { $Mode eq {k} } {
    set KeyF [ tk_getOpenFile -title {tmount - Select a key file} ]
    if { $KeyF ne {} } { append Cmd { -d}
    } else { set Mode {} ; set RetC 1 ; puts stderr Cancelled.
    };#fi
  };#fi

  if { $Mode ne {} } {

    entry .ent -show {*} -textvar Psw ; bind .ent <Return> { set RetC 0 }
    .lbl  configure -text "Enter LUKS passphrase for ${Dev}"
    pack  .ent -fill x -padx 2
    set env(SUDO_ASKPASS) [ file join [ file dirname $SN ] $Ask ]
    set timeout -1 ; log_user 0 ; set Err {}
    set LC $env(LC_ALL) ; set env(LC_ALL) {C}

    if { $Mode eq {k} } {
      spawn {*}$Cmd $KeyF $Dev $Map } else { spawn {*}$Cmd $Dev $Map
    };#fi
    expect {
      {Enter passphrase for *: } {
        set Psw {} ; if [ Dlg ] { set Psw $EOF };#fi
        exp_send "${Psw}\n" ; exp_continue
      }
      full_buffer { append Err $expect_out(buffer) ; exp_continue }
      eof { append Err $expect_out(buffer) }
    };#done
    set RetC [ lrange [ wait ] 3 3 ]

    if { $RetC } { puts stderr $Err
    } else {
      set env(LC_ALL) $LC
      set F [ Prop $Lnk FSTYPE ] ; regsub {^$} $F {(no FS)}    F
      set L [ Prop $Lnk LABEL  ] ; regsub {^$} $L {(no label)} L
      set S [ Prop $Lnk SIZE   ] ; set R [ exec realpath $Lnk ]
      set X "Device %s mapped to %s.\n%s -> %s\n%s (%s, \[%s], %s)\n"
      puts [ format $X $Dev $Map $Lnk $R [ file tail $R ] $F $L $S ]
      set C TMOUNT_Mount_command
      set C [ expr { [ info exist env($C) ] ? $env($C) : {} } ]
      regsub {^$} $C { sh -c "! echo Mounting disabled for ${0} >\&2" } C
      if { ! [ NoFS $R ] } {
        set RetC [ catch { exec -ignorestderr -- {*}$C $R } ]
        if { ! $RetC } { puts "mounted on [ Prop $R MOUNTPOINT ]" };#fi
      };#fi
    };#fi

  };#fi

exit $RetC
